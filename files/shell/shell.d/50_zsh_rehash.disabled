# Taken from Russell Harmon <russ@eatnumber1.com>

# https://github.com/eatnumber1/dotfiles
# https://github.com/eatnumber1/dotfiles/commit/1237e2f880758dc9e819048f51a505a11698a5ce

if is_shell zsh; then
    zmodload -F zsh/stat +b:zstat
    typeset -gi LAST_REHASH
    function rehash-if-needed {
        emulate -L zsh
        setopt err_return warn_create_global
        local rehash_trigger=${TMPDIR:-/tmp}/zsh-needs-rehash
        if [[ -f $rehash_trigger ]]; then
            typeset -A stats
            zstat -H stats +mtime $rehash_trigger
            if [[ $LAST_REHASH -lt $stats[mtime] ]]; then
                hash -r
                LAST_REHASH=$stats[mtime]
            fi
        fi
    }
    add-zsh-hook preexec rehash-if-needed

    function exec-and-trigger-rehash {
        emulate -L zsh
        setopt err_return warn_create_global
        local rehash_trigger=${TMPDIR:-/tmp}/zsh-needs-rehash
        integer code
        local cmd=$1
        shift
        command $cmd "$@" || code=$?
        builtin echo -n > $rehash_trigger
        return $code
    }

    function $0_wrap_if_found {
        local cmd
        for cmd in "$@"; do
            if (( $+commands[$cmd] )); then
                function $cmd {
                    setopt local_options function_argzero
                    exec-and-trigger-rehash $0 "$@"
                }
            fi
        done
    }
    $0_wrap_if_found brew dpkg apt-get aptitude yum rpm pacman yaourt
fi
